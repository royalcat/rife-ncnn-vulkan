// rife implemented with ncnn library

#version 450

#if NCNN_fp16_storage
#extension GL_EXT_shader_16bit_storage: require
#endif
#if NCNN_fp16_arithmetic
#extension GL_EXT_shader_explicit_arithmetic_types_float16: require
#endif

layout (binding = 0) readonly buffer image_blob { sfp image_blob_data[]; };
layout (binding = 1) readonly buffer flow_blob { sfp flow_blob_data[]; };
layout (binding = 2) writeonly buffer top_blob { sfp top_blob_data[]; };

layout (push_constant) uniform parameter
{
    int w;
    int h;
    int c;
    int cstep;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x);
    int gy = int(gl_GlobalInvocationID.y);
    int gz = int(gl_GlobalInvocationID.z);

    if (gx >= p.w || gy >= p.h || gz >= p.c)
        return;

    afp flow_x = buffer_ld1(flow_blob_data, gy * p.w + gx);
    afp flow_y = buffer_ld1(flow_blob_data, p.cstep + gy * p.w + gx);

    afp sample_x = afp(gx) + flow_x;
    afp sample_y = afp(gy) + flow_y;

    // bilinear interpolate
    afp v;
    {
        int x0 = int(floor(sample_x));
        int y0 = int(floor(sample_y));
        int x1 = x0 + 1;
        int y1 = y0 + 1;

        x0 = clamp(x0, 0, p.w - 1);
        y0 = clamp(y0, 0, p.h - 1);
        x1 = clamp(x1, 0, p.w - 1);
        y1 = clamp(y1, 0, p.h - 1);

        afp alpha = sample_x - afp(x0);
        afp beta = sample_y - afp(y0);

        afp v0 = buffer_ld1(image_blob_data, gz * p.cstep + y0 * p.w + x0);
        afp v1 = buffer_ld1(image_blob_data, gz * p.cstep + y0 * p.w + x1);
        afp v2 = buffer_ld1(image_blob_data, gz * p.cstep + y1 * p.w + x0);
        afp v3 = buffer_ld1(image_blob_data, gz * p.cstep + y1 * p.w + x1);

        afp v4 = v0 * (afp(1.f) - alpha) + v1 * alpha;
        afp v5 = v2 * (afp(1.f) - alpha) + v3 * alpha;

        v = v4 * (afp(1.f) - beta) + v5 * beta;
    }

    const int gi = gz * p.cstep + gy * p.w + gx;

    buffer_st1(top_blob_data, gi, v);
}
